# Maintainer:  Dave Murphy <davem@devkitpro.org>

pkgname=3ds-libarchive
pkgver=3.7.2
pkgrel=1
pkgdesc='library that can create and read several streaming archive format (for Nintendo 3DS homebrew development)'
arch=('any')
url="https://www.libarchive.org"
license=('Simplified BSD')
options=(!strip libtool staticlibs)
source=(
 "http://www.libarchive.org/downloads/libarchive-${pkgver}.tar.gz"
 "libarchive-${pkgver}.patch"
)

makedepends=('3ds-pkg-config' 'dkp-toolchain-vars')
depends=('3ds-bzip2' '3ds-zlib' '3ds-liblzma' '3ds-libzstd')
groups=('3ds-portlibs')

build() {
  cd libarchive-$pkgver

  source /opt/devkitpro/3dsvars.sh

  patch -Np1 -i "$srcdir"/libarchive-${pkgver}.patch

  ./configure --disable-shared --enable-static --prefix=${PORTLIBS_PREFIX} --host=arm-none-eabi \
    --disable-shared --enable-static \
    --disable-bsdtar --disable-bsdcat \
    --disable-bsdunzip --disable-bsdcpio --disable-acl \
    --without-xml2 --without-openssl

  make
}

package() {
  cd libarchive-$pkgver

  source /opt/devkitpro/3dsvars.sh

  make DESTDIR="$pkgdir" install
  # license
  install -Dm644 "$srcdir"/libarchive-${pkgver}/COPYING "$pkgdir"${PORTLIBS_PREFIX}/licenses/$pkgname/COPYING
  # remove useless stuff
  rm -r "$pkgdir"${PORTLIBS_PREFIX}/share
  rm -r "$pkgdir"${PORTLIBS_PREFIX}/bin
}

sha256sums=('df404eb7222cf30b4f8f93828677890a2986b66ff8bf39dac32a804e96ddf104'
            '61ee662a2b2fd0daa853d09c78df42cdb23a08a67a9fd19d207f945383aa3dba')
