# Maintainer: Dave Murphy <davem@devkitpro.org>

pkgname="3ds-freetype"
pkgver=2.13.2
pkgrel=1
pkgdesc="TrueType font rendering library (mingw-w64)"
arch=('any')
url="https://www.freetype.org/"
license=(GPL2+ custom:FreeType)
depends=("3ds-bzip2"
         "3ds-libpng"
         "3ds-zlib")
makedepends=('3ds-pkg-config' 'dkp-toolchain-vars')
options=('!strip' '!libtool' 'staticlibs')
source=(https://downloads.sourceforge.net/sourceforge/freetype/freetype-${pkgver}.tar.xz)
groups=('3ds-portlibs')

build() {

  source /opt/devkitpro/3dsvars.sh

  cd freetype-${pkgver}

  ./configure \
    --enable-freetype-config \
    --host=arm-none-eabi \
    --target=arm-none-eabi \
    --prefix=${PORTLIBS_PREFIX} \
    --enable-static \
    --disable-shared \
    --with-zlib \
    --with-bzip2 \
    --without-harfbuzz \
    --with-png

  make
}

package () {

  source /opt/devkitpro/3dsvars.sh

  cd freetype-${pkgver}

  make DESTDIR="${pkgdir}" install

  # patch the config script to always output static libraries when not relying on pkg-config
  sed 's/\$show_static/yes/' -i "${pkgdir}/${PORTLIBS_PREFIX}/bin/freetype-config"

  # Licenses
  install -Dm644 "LICENSE.TXT" "${pkgdir}${PORTLIBS_PREFIX}/licenses/${pkgname}/LICENSE.TXT"
  install -Dm644 "docs/GPLv2.TXT"   "${pkgdir}${PORTLIBS_PREFIX}/licenses/${pkgname}/GPLv2.TXT"
  install -Dm644 "docs/FTL.TXT"     "${pkgdir}${PORTLIBS_PREFIX}/licenses/${pkgname}/FTL.TXT"
}
sha256sums=('12991c4e55c506dd7f9b765933e62fd2be2e06d421505d7950a132e4f1bb484d')
