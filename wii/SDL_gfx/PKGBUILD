# Maintainer:  Dave Murphy <davem@devkitpro.org>

pkgname=wii-sdl_gfx
pkgver=2.0.27
pkgrel=1
pkgdesc="Graphics drawing primitives and other support functions wrapped up in an add-on, C-based library for the Simple Direct Media (SDL) cross-platform API layer"
arch=('any')
url="http://www.ferzkopp.net/wordpress/2016/01/02/sdl_gfx-sdl2_gfx/"
license=("LGPL")
options=(!strip libtool staticlibs)
depends=(
 'wii-sdl'
)
makedepends=(
 'wii-pkg-config'
 'dkp-toolchain-vars'
)
source=(
  "https://sourceforge.net/projects/sdlgfx/files/SDL_gfx-${pkgver}.tar.gz"
)

groups=('wii-portlibs' 'wii-sdl-libs')

build() {
  cd SDL_gfx-$pkgver

  source /opt/devkitpro/wiivars.sh

  ./configure --prefix="${PORTLIBS_PREFIX}" --host=powerpc-eabi \
    --disable-shared --enable-static \
    --disable-mmx

  make
}

package() {
  cd SDL_gfx-$pkgver

  source /opt/devkitpro/wiivars.sh

  make DESTDIR="$pkgdir" install
}

sha256sums=('dfb15ac5f8ce7a4952dc12d2aed9747518c5e6b335c0e31636d23f93c630f419')
