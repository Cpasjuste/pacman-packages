# Maintainer:  Dave Murphy <davem@devkitpro.org>
# Contributor: Sergio Padrino <sergio.padrino@gmail.com>

pkgname=wii-sdl
pkgver=1.2.15
pkgrel=13
pkgdesc="A library for portable low-level access to a video framebuffer, audio output, mouse, and keyboard"
arch=('any')
url="https://libsdl.org"
license=("LGPL")
options=(!strip libtool staticlibs)
makedepends=('wii-pkg-config' 'dkp-toolchain-vars')
source=("${url}/release/SDL-${pkgver}.tar.gz" "SDL-1.2.15.patch" "config.site")
groups=('wii-portlibs' 'wii-sdl-libs')

build() {
  cd SDL-$pkgver

  source /opt/devkitpro/wiivars.sh

  patch -p1 -i $srcdir/SDL-1.2.15.patch
  ./autogen.sh

  CONFIG_SITE=${srcdir}/config.site ./configure --prefix="${PORTLIBS_PREFIX}" --host=powerpc-eabi \
    --disable-shared --enable-static --enable-nintendo-wii
  make
}

package() {
  cd SDL-$pkgver

  source /opt/devkitpro/wiivars.sh

  make DESTDIR="$pkgdir" install

  # remove useless stuff
  rm -r "$pkgdir"${PORTLIBS_PREFIX}/share
}

sha256sums=('d6d316a793e5e348155f0dd93b979798933fb98aa1edebcc108829d6474aad00'
            'c06fc22d551477927bb571423ecd94072288b2fe70de5dc2ccc3cc3249a5b006'
            'eb33978edaeeb22f83d108ea5236e7f749c3fb3d60765e69f624af2365e2b854')
sha256sums=('d6d316a793e5e348155f0dd93b979798933fb98aa1edebcc108829d6474aad00'
            '82b3b2d3964457d7764abf5590eedb8881b77cbe9d6104d5d5de02ffdff2097c'
            'eb33978edaeeb22f83d108ea5236e7f749c3fb3d60765e69f624af2365e2b854')
sha256sums=('d6d316a793e5e348155f0dd93b979798933fb98aa1edebcc108829d6474aad00'
            '5aa8e71f31cc8d10fb586f00a79f0765aed047d6573a02e218034a03606eb90e'
            'eb33978edaeeb22f83d108ea5236e7f749c3fb3d60765e69f624af2365e2b854')
