# Maintainer: Dave Murphy <davem@devkitpro.org>
# Contributor: cpasjuste <cpasjuste@gmail.com>

pkgname=switch-libdsm
pkgver=0.3.0
pkgrel=1
pkgdesc='Minimalist smb client'
arch=('any')
url="https://github.com/videolabs/libdsm"
license=('LGPL2.1')
options=(!strip staticlibs)
makedepends=('switch-pkg-config' 'devkitpro-pkgbuild-helpers')
source=("https://github.com/videolabs/libdsm/releases/download/v$pkgver/libdsm-$pkgver.tar.gz")
sha256sums=('96caef854a1f7aa61ab6fdfca9c36a1813b37d23d013db0bc4ec7cd38a570b1c')
groups=('switch-portlibs')
depends=('switch-libtasn1')

prepare() {
  cd libdsm-$pkgver
  patch -Np1 < ../../libdsm.patch
}

build() {
  cd libdsm-$pkgver

  source /opt/devkitpro/devkita64.sh
  source /opt/devkitpro/switchvars.sh

  ./configure --prefix="$PORTLIBS_PREFIX" --host=aarch64-none-elf \
    --disable-shared --enable-static --disable-programs

  make
}

package() {
  cd libdsm-$pkgver

  source /opt/devkitpro/devkita64.sh
  source /opt/devkitpro/switchvars.sh

  make DESTDIR="$pkgdir" install

  # remove documentation
  rm -fr "$pkgdir"${PORTLIBS_PREFIX}/share
}
