# Maintainer:  Dave Murphy <davem@devkitpro.org>

pkgname=switch-sdl_image
pkgver=1.2.12
pkgrel=1
pkgdesc="A simple library to load images of various formats as SDL surfaces (Nintendo 3DS port)"
arch=('any')
url="https://libsdl.org"
license=("LGPL")
options=(!strip libtool staticlibs)
depends=(
 'switch-sdl'
 'switch-libpng'
 'switch-libjpeg-turbo'
)
makedepends=(
 'switch-pkg-config'
 'devkitpro-pkgbuild-helpers'
 'devkitA64'
)
source=("${url}/projects/SDL_image/release/SDL_image-${pkgver}.tar.gz" 'config.sub' 'config.guess')

sha256sums=(
 '0b90722984561004de84847744d566809dbb9daf732a9e503b91a1b5a84e5699'
 '72e02ea93447038f8ced24f296b31e0f397bbcc6b32abdcf9b38c80f153433fd'
 'fbc2337aa59a204f5d74743b82c8be7aab8b39853b4e54a888008f70430c4305'
)

groups=('switch-portlibs')

build() {
  cd SDL_image-$pkgver

  cp $srcdir/config.sub .
  cp $srcdir/config.guess .

  ./autogen.sh

  source /opt/devkitpro/devkita64.sh
  source /opt/devkitpro/switchvars.sh

  ./configure --prefix="${PORTLIBS_PREFIX}" --host=aarch64-none-elf \
    --disable-shared --enable-static
  make
}

package() {
  cd SDL_image-$pkgver

  source /opt/devkitpro/devkita64.sh
  source /opt/devkitpro/switchvars.sh

  make DESTDIR="$pkgdir" install

}
