# Maintainer: WinterMute <davem@devkitpro.org>
pkgname=ppc-zlib
pkgver=1.2.13
pkgrel=1
pkgdesc='Deflate compression method library'
arch=('any')
url='http://www.zlib.net'
license=('zlib')
options=(!strip libtool staticlibs)
source=("${url}/zlib-$pkgver.tar.xz")
sha256sums=(
  'd14c38e313afc35a9a8760dadf26042f51ea0f5d154b0630a31da0540107fb98'
)
makedepends=('dkp-toolchain-vars')
groups=('ppc-portlibs')
build() {
  cd zlib-$pkgver

  source /opt/devkitpro/devkitppc.sh
  source /opt/devkitpro/ppcvars.sh

  CHOST=powerpc-eabi \
    ./configure --prefix="/opt/devkitpro/portlibs/ppc" --static
  make libz.a # only build library, no programs
}

package() {
  cd zlib-$pkgver

  source /opt/devkitpro/devkitppc.sh

  make DESTDIR="$pkgdir" install
  # license
  install -d "$pkgdir"/opt/devkitpro/portlibs/ppc/licenses/$pkgname
  tail -n 31 README > "$pkgdir"/opt/devkitpro/portlibs/ppc/licenses/$pkgname/LICENSE
  # remove useless stuff
  rm -r "$pkgdir"/opt/devkitpro/portlibs/ppc/share
}
