# Maintainer:  Dave Murphy <davem@devkitpro.org>

pkgname=ppc-flac
pkgver=1.3.2
pkgrel=1
pkgdesc='FLAC stands for Free Lossless Audio Codec, an audio format similar to MP3, but lossless...'
arch=('any')
url='https://xiph.org/flac/'
license=(Xiph.org)
options=(!strip libtool staticlibs)
depends=("switch-libogg")
makedepends=('dkp-toolchain-vars')
source=(
  "https://ftp.osuosl.org/pub/xiph/releases/flac/flac-$pkgver.tar.xz"
  "flac-${pkgver}.patch"
)
groups=('ppc-portlibs')

sha256sums=(
  '91cfc3ed61dc40f47f050a109b08610667d73477af6ef36dcad31c31a4a8d53f'
  '7ddddd4d96ff19b5e41f2fbcbff6cf78b29669b25c10e7c04cb18fd0e53d4fa5'
)

build() {
  cd flac-$pkgver

  source /opt/devkitpro/ppcvars.sh

  patch -Np1 -i "$srcdir/flac-${pkgver}.patch"

  CFLAGS="${CFLAGS} -D__WIIU__ " \
    ./configure --prefix="${PORTLIBS_PREFIX}" --host=powerpc-eabi \
    --disable-shared --enable-static \
    --disable-xmms-plugin --enable-cpplibs \
    --disable-sse

  make -C src/libFLAC
}

package() {
  cd flac-$pkgver

  make DESTDIR="$pkgdir" install
}

