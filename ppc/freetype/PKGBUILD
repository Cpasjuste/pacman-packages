# Maintainer:  Dave Murphy <davem@devkitpro.org>

pkgname=ppc-freetype
pkgver=2.13.2
pkgrel=3
pkgdesc='FreeType is a freely available software library to render fonts.'
arch=('any')
url='https://www.freetype.org/'
license=(GPL2+ custom:FreeType)
options=(!strip libtool staticlibs)
groups=('ppc-portlibs')
depends=("ppc-bzip2"
         "ppc-libpng"
         "ppc-zlib"
)
makedepends=('ppc-pkg-config' 'dkp-toolchain-vars')
source=("http://download.savannah.gnu.org/releases/freetype/freetype-$pkgver.tar.xz")

build() {
  cd freetype-$pkgver

  source /opt/devkitpro/ppcvars.sh

  ./configure --prefix="${PORTLIBS_PREFIX}" --host=powerpc-eabi \
    --enable-freetype-config \
    --disable-shared --enable-static \
    --with-zlib \
    --with-bzip2 \
    --without-harfbuzz \
    --with-png

  make
}

package() {
  cd freetype-$pkgver

  source /opt/devkitpro/ppcvars.sh

  make DESTDIR="$pkgdir" install

  # patch the config script to always output static libraries when not relying on pkg-config
  sed 's/\$show_static/yes/' -i "${pkgdir}/${PORTLIBS_PREFIX}/bin/freetype-config"

  # Licenses
  install -Dm644 "LICENSE.TXT" "${pkgdir}/${PORTLIBS_PREFIX}/licenses/${pkgname}/LICENSE.TXT"
  install -Dm644 "docs/GPLv2.TXT"   "${pkgdir}/${PORTLIBS_PREFIX}/licenses/${pkgname}/GPLv2.TXT"
  install -Dm644 "docs/FTL.TXT"     "${pkgdir}/${PORTLIBS_PREFIX}/licenses/${pkgname}/FTL.TXT"

}
sha256sums=('12991c4e55c506dd7f9b765933e62fd2be2e06d421505d7950a132e4f1bb484d')
